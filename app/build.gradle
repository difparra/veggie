plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.diegoparra.veggie"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 5
        versionName "1.5"
        //  Desugaring setUp: https://developer.android.com/studio/write/java8-support#groovy
        //  Desugaring java.time / Required when setting minSdkVersion to 20 or lower
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // Desugaring java.time / Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }
    /*  // If necessary to solve error when running android test
    configurations.all {
        resolutionStrategy {
            exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
        }
    }
     */
}

dependencies {

    //  Modules
    implementation project(':core')
    implementation project(':products')
    implementation project(':auth')

    //  Basic dependencies
    implementation "androidx.appcompat:appcompat:$appCompat"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.core:core-ktx:$coreKtx"
    implementation "com.jakewharton.timber:timber:$timber"
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutines"
    // Desugaring java.time / Desugaring support for api<26 to work with java.time classes
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'


    //  DATA DEPENDENCIES

    //  Data - Room & datastore (prefs)
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"           //  Coroutines support for room
    implementation "androidx.datastore:datastore-preferences:$dataStoreVersion"

    //  Firebase
    implementation platform("com.google.firebase:firebase-bom:$firebaseBomVersion")      //  Manage all Firebase Library versions
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$firebaseCoroutines"     // await function to work with suspend and coroutines
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'        //  RemoteConfig
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'     //  For notifications reviews in firebase console
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation "com.google.android.gms:play-services-auth:$googleAuth"      //  For google auth
    implementation "com.facebook.android:facebook-login:$facebookAuth"          //  For facebook auth
    //  More firebase products https://firebase.google.com/docs/android/setup#available-libraries


    //  UI DEPENDENCIES

    //  LiveData & ViewModel
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"            //  LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hiltViewModel"                  //  In order to inject viewModels
    kapt "androidx.hilt:hilt-compiler:$hiltViewModelCompiler"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycleVersion"    //  Saved state module for viewModel

    //  Ui elements
    implementation "com.google.android.material:material:$materialVersion"             //  Material design
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.viewpager2:viewpager2:$viewPager2"
    implementation "androidx.recyclerview:recyclerview:$recyclerView"
    implementation "io.coil-kt:coil:$coil"

    //  Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.hilt:hilt-navigation-fragment:$navHiltVersion" //  In order to scope hiltNavGraphViewModels


    //  TEST DEPENDENCIES

    //  Testing
    testImplementation "junit:junit:$junit"
    testImplementation "com.google.truth:truth:$truth"                       //  Easily write assertions
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesTest"        //  Kotlin coroutines test
    //  Instrumented test
    androidTestImplementation "androidx.test.ext:junit:$androidJunit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "com.google.truth:truth:$truth"                //  Easily write assertions
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesTest"        //  Kotlin coroutines test

    /*
    testImplementation 'androidx.arch.core:core-testing:2.1.0'  //  Test helpers for LiveData
    testImplementation 'androidx.test:core:1.3.0'               //  Roboelectric environment
    testImplementation 'org.mockito:mockito-core:2.19.0'        //  Mockito framework
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'        //  Kotlin coroutines test
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.0.0-beta01'       // UI Tests Compose
    androidTestImplementation "com.google.android.gms:play-services-gcm:16.0.0"
     */
}